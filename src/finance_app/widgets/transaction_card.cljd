(ns finance-app.widgets.transaction-card
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter.alpha2 :as f]
   [finance-app.widgets.bottom-sheet :as bottom-sheet]
   [finance-app.utils.icons :as icons]
   [finance-app.utils.colors :as colors]))

(defn card [height t]
  (f/widget
   :context context
   (m/GestureDetector 
    .onTap (fn [] (bottom-sheet/show-transaction context t)))
   (m/Container
    .padding (.symmetric m/EdgeInsets .vertical 10)
    .width 65
    .decoration
    (m/BoxDecoration
     .color (-> m/Theme (.of context) .-cardColor)
     .borderRadius (.circular m/BorderRadius 12)))
   (m/Column
    .crossAxisAlignment m.CrossAxisAlignment/center
    .children
    [(m/Icon (get icons/icons (get t "icon_name"))
             .color colors/green)
     (m/SizedBox .height height)
     (m/Row
      .crossAxisAlignment m.CrossAxisAlignment/start
      .mainAxisAlignment m.MainAxisAlignment/center
      .children
      [(m/Column
        .mainAxisAlignment m.MainAxisAlignment/spaceBetween
        .children
        [(mgl/MongolText (get t "category_name"))
         (mgl/MongolText
          (subs
           (get t "transaction_time") 0 10)
          .style (m/TextStyle .fontSize 12))])
       (mgl/MongolText (if (empty? (get t "content"))
                         ""
                         (get t "content"))
                       .style (m/TextStyle .fontSize 16))
       (m/SizedBox .width 2)])
     (m/Spacer)
     (mgl/MongolText
      (str
       (if (= 1 (get t "main_type"))
         "- "
         "+ ")
       (-> (get t "amount") str))
      .style (m/TextStyle
              .fontSize 18
              .fontWeight m.FontWeight/bold
              .color (if (= 1 (get t "main_type"))
                       m.Colors/red
                       m.Colors/green)))])))