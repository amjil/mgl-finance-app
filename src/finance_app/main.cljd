(ns finance-app.main
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter_dotenv/flutter_dotenv.dart" :as dotenv]
   ["package:adaptive_theme/adaptive_theme.dart" :as adaptive]
   ["package:keyboard_height_plugin/keyboard_height_plugin.dart" :as keyboard-plugin]
   [cljd.flutter :as f]
   [virtual-keyboard.options :as keyboard-options]
   [virtual-keyboard.input-control :as control]
   [finance-app.states.global :as gs]
   [finance-app.states.transactions :as transactions]
   [finance-app.states.profile :as profile]
   [finance-app.utils.themes :as themes]
   [finance-app.utils.datetime :as datetime]
   [finance-app.widgets.navbar :as nav]))

(defn main []
  (m.WidgetsFlutterBinding/ensureInitialized)
  (await (.load dotenv/dotenv .fileName ".env"))
  
  (let [curr-month (datetime/current-year-month "yyyy-MM-dd HH:mm:ss")]
    (await (transactions/query-transactions curr-month)))
  
  ;;
  (profile/prepopulate-profile-settings)

  ;; Set up input control
  (control/set-control)
  
  ;; keyboard height
  (let [keyboard-height-plugin (keyboard-plugin/KeyboardHeightPlugin)]
    (.onKeyboardHeightChanged
     keyboard-height-plugin
     (fn [height]
       (swap! gs/state assoc :keyboard-height height))))
  
  (let [initial-theme (await (themes/initial-theme))] 
    (m/runApp
     (f/widget
      :context ctx
      :let [width  (-> m/MediaQuery (.of ctx) .-size .-width)
            info (merge keyboard-options/keyboard-option
                        {:keyboard/key-cap-border-radius 6
                        ;;  :keyboard/key-container-color (m/Colors.black 100)
                        ;;  :keyboard/action-keys-container-color (m/Colors.black 100)
                        ;;  :keyboard/key-text-color (m/Colors.grey 400)
                        ;;  :keyboard/action-text-color (m/Colors.grey 400)
                         }
                        {:keyboard/width width})]
      :bind {:info info :state gs/state}
      (adaptive/AdaptiveTheme
       .light themes/light-theme
       .dark themes/dark-theme
       .initial initial-theme
       .builder (fn [light dark]
                  (m/MaterialApp
                   .title "Welcome to Flutter"
                   .theme light
                   .darkTheme dark
                  ;; .home (nav/bottom-nav)
                   .initialRoute "/"
                   .routes {"/" nav/screen}
                   .debugShowCheckedModeBanner false)))))))
